##### 1： 向表中装载数据（Load）

##### 1.1: 语法简介

```sql
hive> load data [local] inpath '/opt/module/datas/student.txt' overwrite | into table student [partition (partcol1=val1,…)];

（1）load data:表示加载数据
（2）local:表示从本地加载数据到hive表；否则从HDFS加载数据到hive表
（3）inpath:表示加载数据的路径
（4）overwrite:表示覆盖表中已有数据，否则表示追加
（5）into table:表示加载到哪张表
（6）student:表示具体的表
（7）partition:表示上传到指定分区
```

##### 1.2： 实例操作

```sql
-- 方式一： 加载本地数据到hive
-- 1.2.1: 创建一张表
hive (default)> create table student(id string, name string) row format delimited fields terminated by '\t';
--1.2.2:  加载本地文件到hive
hive (default)> load data local inpath '/opt/module/datas/student.txt' into table default.student;


-- 方式二： 加载HDFS文件到hive中
-- 1.2.1： 上传文件到HDFS
hive (default)> dfs -put /opt/module/datas/student.txt /user/atguigu/hive;
-- 1.2.2： 加载HDFS上数据
hive (default)> load data inpath '/user/atguigu/hive/student.txt' into table default.student;

-- 方式三： 加载数据覆盖表中已有的数据
-- 上传文件到HDFS
hive (default)> dfs -put /opt/module/datas/student.txt /user/atguigu/hive;
-- 加载数据覆盖表中已有的数据
hive (default)> load data inpath '/user/atguigu/hive/student.txt' overwrite into table default.student;
```

##### 2： 通过查询语句向表中插入数据（Insert）

```sql
-- 2.1: 基本模式插入
hive (default)> insert into table  student partition(month='201709') values(1,'wangwu');

--2.2： 基本模式插入（根据单张表查询结果）
hive (default)> insert overwrite table student partition(month='201708')
             select id, name from student where month='201709';
-- 多插入模式（根据多张表查询结果）
hive (default)> from student
              insert overwrite table student partition(month='201707')
              select id, name where month='201709'
              insert overwrite table student partition(month='201706')
              select id, name where month='201709';
```

##### 3： 查询语句中创建表并加载数据（As Select）

```sql
create table if not exists student3
as select id, name from student;
```

##### 4: 创建表时通过Location指定加载数据路径

```sql
-- 4.1： 上传数据到hdfs上
hive (default)> dfs -put /opt/module/datas/student.txt
/user/hive/warehouse/student5;

-- 4.2: 创建表，并指定在hdfs上的位置
hive (default)> create table if not exists student5(
              id int, name string
              )
              row format delimited fields terminated by '\t'
              location '/user/hive/warehouse/student5';

```

##### 5： Import数据到指定Hive表

```sql
-- 注意：先用export导出后，再将数据导入。
hive (default)> import table student2 partition(month='201709') from
 '/user/hive/warehouse/export/student';
```

